<html>
  <head>
    <script type="text/javascript" src="gl-matrix.js"></script>
    <script type="text/javascript" src="renderer.js"></script>
    <script type="x-shader/x-vertex" id="vertex-shader">
      //
      // Description : Array and textureless GLSL 2D/3D/4D simplex
      //               noise functions.
      //      Author : Ian McEwan, Ashima Arts.
      //  Maintainer : stegu
      //     Lastmod : 20110822 (ijm)
      //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
      //               Distributed under the MIT License. See LICENSE file.
      //               https://github.com/ashima/webgl-noise
      //               https://github.com/stegu/webgl-noise
      //

      vec3 mod289(vec3 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 mod289(vec4 x) {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x) {
           return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      float snoise(vec3 v)
      {
        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

      // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

      // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //   x0 = x0 - 0.0 + 0.0 * C.xxx;
        //   x1 = x0 - i1  + 1.0 * C.xxx;
        //   x2 = x0 - i2  + 2.0 * C.xxx;
        //   x3 = x0 - 1.0 + 3.0 * C.xxx;
        vec3 x1 = x0 - i1 + C.xxx;
        vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
        vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

      // Permutations
        i = mod289(i);
        vec4 p = permute( permute( permute(
                   i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                 + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
                 + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

      // Gradients: 7x7 points over a square, mapped onto an octahedron.
      // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
        float n_ = 0.142857142857; // 1.0/7.0
        vec3  ns = n_ * D.wyz - D.xzx;

        vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);

        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );

        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));

        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

      //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

      // Mix final noise value
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
                                      dot(p2,x2), dot(p3,x3) ) );
      }

      attribute vec4 aPosition;

      uniform mat4 uVMatrix;
      uniform mat4 uMVMatrix;
      uniform mat4 uPMatrix;
      uniform mat3 uNormalMatrix;
      uniform bool uUseLighting;
      uniform float uRandomSeedX;
      uniform float uRandomSeedY;
      uniform float uRandomSeedZ;
      uniform float uScale;
      uniform float uFrequency;

      varying vec3 vColor;
      varying vec3 vNormal;
      varying vec3 vPosition;

      vec3 fromRgb(int r, int g, int b) {
        return vec3(float(r), float(g), float(b)) / 255.0;
      }

      void main() {
        // snoise generates values from -1 to 1
        float offset = snoise(vec3(
          uFrequency * aPosition.x + uRandomSeedX,
          uFrequency * aPosition.y + uRandomSeedY,
          uFrequency * aPosition.z + uRandomSeedZ
        ));

        // Generate an offset factor. We will make this
        // value between 1 - scale and 1 + scale, inclusive
        float factor = 1.0 + offset * uScale;

        // Deform the vertex along its normal
        vec4 newPoint = aPosition;
        vec4 normal = normalize(aPosition);
        newPoint.x = newPoint.x + factor * normal.x;
        newPoint.y = newPoint.y + factor * normal.y;
        newPoint.z = newPoint.z + factor * normal.z;

        gl_Position = uPMatrix * uMVMatrix * newPoint;

        // If we're on land:
        if (offset > 0.0) {
          if (offset > 0.5) {
            vColor = fromRgb(105, 105, 105); // dark grey
          } else {
            vColor = fromRgb(0, 100, 0) * (offset + 0.5); // dark green
          }
        }

        // We're underwater now!
        if (offset < 0.0) {
          vec3 blue = fromRgb(0, 0, 139);

          if (offset > -0.5) {
            vColor = blue;
          } else { // we're in deeper water, so let's make the color a bit darker
            vColor = blue * 0.7;
          }
        }

        // Make the poles ice!
        if (abs(aPosition.y) > 0.9) {
          vColor = fromRgb(220, 220, 220) * smoothstep(0.75, 1.0, abs(aPosition.y));
        }

        vNormal = uNormalMatrix * normal.xyz;
        vPosition = vec3(uMVMatrix * aPosition);
      }
    </script>
    <script type="x-shader/x-fragment" id="fragment-shader">
      precision mediump float;

      uniform bool uUseLighting;
      uniform vec3 uLightColor;
      uniform vec3 uLightPos;
      uniform float uDiffuseCoefficient;
      uniform float uMinLight;

      varying vec3 vColor;
      varying vec3 vNormal;
      varying vec3 vPosition;

      void main() {
        vec3 color = vec3(0.0);

        if (uUseLighting) {
          vec3 newNormal = normalize(vNormal);
          vec3 normalizedLight = normalize(uLightPos - vPosition);
          float diffuse = max(dot(vNormal, normalizedLight), uMinLight);
          color = uDiffuseCoefficient * vColor * diffuse * uLightColor;
        } else {
          color = vColor;
        }

        gl_FragColor = vec4(color, 1.0);
      }
    </script>
  </head>
  <!-- Inspiration for configuration section taken from WebGL tutorial
      at this website: http://learningwebgl.com/lessons/lesson13/index.html -->
  <body onload="renderSphere();">
    <div>
      <canvas id="myCanvas" width="640px" height="640px" style="border:5px solid lightgray;"> </canvas>

      <h3> Lighting </h3>
      <br>
      <input type="checkbox" id="use-lighting" checked> Use Lighting

      <br>
      <table>
        <tr>
          <td>Light Location: </td>
          <td>X: <input id='light-pos-x' value='5'></td>
          <td>Y: <input id='light-pos-y' value='5'></td>
          <td>Z: <input id='light-pos-z' value='5'></td>
        </tr>
      </table>

      Light Intensity: <input type="float" id="diffuse-coefficient" value="2.0">
      <br>
      Minimum Light: <input type="float" id="min-light" value="0.1">

      <h3> Noise Parameters </h3>

      <br>
      Scale: <input id='scale' value='0.25'>

      <br>
      Frequency: <input id='frequency' value='2.0'>

      <br>

      <h3> Description </h3>
      <p>
        This is a 3d world generator! Every time you refresh the page, you
        should see a new randomly generated world. Higher points will be colored grey,
        like moutains. All other land will be colored green. Lower points will be
        colored blue, in order to resemble oceans, with the lowest parts being a darker
        blue than the rest. I decided to color the north/south poles white to resemble ice,
        like on Earth.

        <br>
        <br>


        You can mess with some parameters above to give your world a new
        personality.  Change the light intensity to brighten/darken your
        planet.  You can add more (albeit jagged) mountains by increasing the
        frequency. You can also increase the height/depth of the deformations
        by modifying the scale (a little bit goes a long way!)

      </p>
    </div>
  </body>
</html>
